[[model]]
name: Pathological tissue ttp-2006 model
author: Michael Clerx
desc: """
    Pathological version of the 2006 model by ten Tusscher & Panfilov adapted
    for tissue simulations
    
    Modifications for transmural simulations:
        - Added l'hopital rule formulation for ical.ICaL to allow single
          precision calculations.
    Pathological modifications:
        - Lowered ical.gCaL to 50%
        - Increased epicardial ito.gTo to about 240%
        - Lowered conductance (see script tab)
        - Heterogenous gCaL (see script tab)
    Extra modifications:
        - Set a new initial state based on 400ms cycle length

    
    """
# Initial values
membrane.V      = -8.53512226372068170e+01
calcium.Ca_i    =  1.04220604028414134e-04
calcium.Ca_sr   =  3.49573387848614914e+00
calcium.Ca_ss   =  2.12961856004305344e-04
calcium.R_prime =  9.89158052094092022e-01
sodium.Na_i     =  9.48609784117166299e+00
potassium.K_i   =  1.35557487658997019e+02
ikr.xr1         =  2.06899352993403902e-04
ikr.xr2         =  4.72435813440243491e-01
iks.xs          =  3.23564410243259862e-03
ina.m           =  1.67181849516679065e-03
ina.h           =  7.48213521810986548e-01
ina.j           =  7.47828620444365266e-01
ical.d          =  3.31812774199498795e-05
ical.f          =  9.76714692369792248e-01
ical.f2         =  9.99411002887193756e-01
ical.fCass      =  9.99974272857896751e-01
ito.s           =  9.99997904693742057e-01
ito.r           =  2.35709886030767176e-08

#
# Fast Sodium current INa
#
# Appendix to [3]
#
[ina]
use membrane.V
gNa = 14.838 [mS/uF]
INa = gNa * m ^ 3 * h * j * (V - rev.ENa)
    in [A/F]
dot(m) = (inf - m) / tau
    inf = 1 / (1 + exp((-56.86 - V) / 9.03)) ^ 2
    alpha = 1 / (1 + exp((-60 - V) / 5))
    beta = 0.1 / (1 + exp((V + 35) / 5)) + 0.1 / (1 + exp((V - 50) / 200))
    tau = alpha * beta
dot(h) = (inf - h) / tau
    inf = 1 / (1 + exp((V + 71.55) / 7.43)) ^ 2
    alpha = if(V < -40,
        0.057 * exp(-(V + 80) / 6.8),
        0)
    beta = if(V < -40,
        2.7 * exp(0.079 * V) + 310000 * exp(0.3485 * V),
        0.77 / (0.13 * (1 + exp((V + 10.66) / -11.1))))
    tau = 1 / (alpha + beta)
dot(j) = (inf - j) / tau
    inf = 1 / (1 + exp((V + 71.55) / 7.43)) ^ 2
    alpha = if(V < -40,
        (-25428 * exp(0.2444 * V) - 6.948e-6 * exp(-0.04391 * V)) * (V + 37.78) / 1 / (1 + exp(0.311 * (V + 79.23))),
        0)
    beta = if(V < -40,
        0.02424 * exp(-0.01052 * V) / (1 + exp(-0.1378 * (V + 40.14))),
        0.6 * exp(0.057 * V) / (1 + exp(-0.1 * (V + 32))))
    tau = 1 / (alpha + beta)

#
# Transient outward current ITo with voltage-shifted dynamics of activation and
# inactivation.
#
# Appendix to [2]
#
[ito]
use membrane.V
# Increase epicardial gTo
gTo = if(cell.type == 0, 0.073, if(cell.type == 2, 0.294, 0.9))
    in [mS/uF]
ITo = gTo * r * s * (V - rev.EK)
    in [A/F]
dot(r) = (inf - r) / tau
    # Voltage shift in activation to further increase ITo
    v = V + 20
    inf = 1 / (1 + exp((20 - v) / 6))
    tau = 9.5 * exp(-(v + 40) ^ 2 / 1800) + 0.8
dot(s) = (inf - s) / tau
    # Smaller shift in inactivation
    v = V + 10
    inf = if(cell.type == 0,
        1 / (1 + exp((v + 28) / 5)),
        1 / (1 + exp((v + 20) / 5)))
    tau = if(cell.type == 0,
        1000 * exp(-(v + 67) ^ 2 / 1000) + 8,
        85 * exp(-(v + 45) ^ 2 / 320) + 5 / (1 + exp((v - 20) / 5)) + 3)

#
# L-type calcium current
#
# Appendix to [2]
#
[ical]
use membrane.V
# Lowered gCaL (original 3.98e-5)
gCaL = 2.0e-5
# Adapted this using l'Hopitals rule for V = 15 
ICaL = if(abs(V - 15) < 1e-6,
        c1 * (c2 - c4) / c3,
        c1 * (V - 15) * (c2 * exp(c3 * (V - 15)) - c4) / (exp(c3 * (V - 15)) - 1))
    in [A/F]
    c1 = gCaL * d * f * f2 * fCass * 4 * phys.FFTR
    c2 = 0.25 * calcium.Ca_ss
    c3 = 2 * phys.FTR
    c4 = extra.Cao
dot(d) = (inf - d) / tau
    alpha = 1.4 / (1 + exp((-35 - V) / 13)) + 0.25
    beta  = 1.4 / (1 + exp((V + 5) / 5))
    gamma = 1 / (1 + exp((50 - V) / 20))
    tau = alpha * beta + gamma
    inf = 1 / (1 + exp((-8 - V) / 7.5))
dot(f2) = (inf - f2) / tau
    inf = 0.67 / (1 + exp((V + 35) / 7)) + 0.33
    tau = 562 * exp(-(V + 27) ^ 2 / 240) + 31 / (1 + exp((25 - V) / 10)) + 80 / (1 + exp((V + 30) / 10))
dot(fCass) = (inf - fCass) / tau
    inf = 0.6 / (1 + (calcium.Ca_ss / 0.05) ^ 2) + 0.4
    tau = 80 / (1 + (calcium.Ca_ss / 0.05) ^ 2) + 2
dot(f) = (inf - f) / tau
    inf = 1 / (1 + exp((V + 20) / 7))
    tau = 1102.5 * exp(-(V + 27) ^ 2 / 225) + 200 / (1 + exp((13 - V) / 10)) + 180 / (1 + exp((V + 30) / 10)) + 20
    
#
# External inputs
#
[engine]
pace = 0 bind pace
time = 0 bind time
    in [ms]    

#
# Cell type (endo. epi, mid)
#
# 0 : endocardial
# 1 : epicardial
# 2 : midmyocardial
#
[cell]
type = 1
    desc: The cell type

#
# Membrane potential
#
# Appendix to [3]
#
[membrane]
use stimulus.i_stim
dot(V) = -(i_ion + i_stim + i_diff)
    in [mV]
    label membrane_potential
i_ion = potassium.ItK + calcium.ItCa + sodium.ItNa
i_diff = 0
    bind diffusion_current
    in [A/F]

#
# Physical constants
#
# Appendix to [3]
#
[phys]
F =  96485.3415 [C/mmol]
R = 8314.472 [J/mol/K]
T = 310 [K]
RTF = R * T / F
FTR = F / (R * T)
FFTR = F * F / (R * T)
Cm = 0.185 [uF]
Vc = 0.016404 [um^3]

#
# Stimulus current
#
# Appendix to [3]
#
[stimulus]
i_stim = amplitude * engine.pace
    amplitude = -52 [A/F]

#
# Reversal potentials
#
# Appendix to [3]
#
[rev]
use phys.RTF
use membrane.V
P_kna = 0.03
ECa = 0.5 * RTF * log(extra.Cao / calcium.Ca_i)
    in [mV]
EK = RTF * log(extra.Ko / potassium.K_i)
    in [mV]
EKs = RTF * log((extra.Ko + P_kna * extra.Nao) / (potassium.K_i + P_kna * sodium.Na_i))
    in [mV]
ENa = phys.RTF * log(extra.Nao / sodium.Na_i)
    in [mV]
    
#
# External concentrations
#
# Appendix to [3]
#
[extra]
Ko = 5.4 [mM]
Nao = 140.0 [mM]
Cao = 2 [mM]

#
# Background Calcium current ICaB
#
# Appendix to [3]
#
[icab]
use membrane.V
g_bca = 0.000592 [mS/uF]
ICaB = g_bca * (V - rev.ECa)
    in [A/F]

#
# Calcium pump IpCa
#
# Appendix to [3]
#
[ipca]
use calcium.Ca_i
K_pCa = 0.0005
    in [mM]
g_pCa = 0.1238
    in [A/F]
IpCa = g_pCa * Ca_i / (Ca_i + K_pCa)
    in [A/F]

#
# Inward rectifier Potassium current IK1
#
# Appendix to [3]
#
[ik1]
use rev.EK
use membrane.V
gK1 = 5.405 [mS/uF]
IK1 = gK1 * inf * sqrt(extra.Ko / 5.4) * (V - rev.EK)
    in [A/F]
inf = alpha / (alpha + beta)
    alpha = 0.1 / (1 + exp(0.06 * (V - EK - 200)))
    beta = (3.0 * exp(0.0002 * (V - EK + 100)) + exp(0.1 * (V - EK - 10))) / (1 + exp(-0.5 * (V - EK)))

#
# Potassium pump IpK
#
# Appendix to [3]
#
[ipk]
use membrane.V
gpK = 0.0146 [mS/uF]
IpK = gpK * (V - rev.EK) / (1 + exp((25 - V) / 5.98))
    in [A/F]

#
# Rapid time dependent Potassium current IKr
#
# Appendix to [3]
#
[ikr]
use membrane.V
use extra.Ko
gKr = 0.153 [mS/uF]
IKr = gKr * sqrt(Ko / 5.4) * xr1 * xr2 * (V - rev.EK)
    in [A/F]
dot(xr1) = (inf - xr1) / tau
    alpha = 450 / (1 + exp((-45 - V) / 10))    
    beta = 6 / (1 + exp((V + 30) / 11.5))
    tau = 1 * alpha * beta
    inf = 1 / (1 + exp((-26 - V) / 7))
dot(xr2) = (inf - xr2) / tau
    alpha = 3 / (1 + exp((-60 - V) / 20))
    beta = 1.12 / (1 + exp((V - 60) / 20))
    tau = 1 [ms] * alpha * beta
    inf = 1 / (1 + exp((V + 88) / 24))

#
# Slow time dependent Potassium current IKs
#
# Appendix to [2]
#
[iks]
use membrane.V
gKs = if(cell.type == 2, 0.098, 0.392)
IKs = gKs * xs * xs * (V - rev.EKs)
    in [A/F]
dot(xs) = (inf - xs) / tau
    alpha = 1400 / sqrt(1 + exp((5 - V) / 6))
    beta  = 1 / (1 + exp((V - 35) / 15))
    tau = 1 * alpha * beta + 80
    inf = 1 / (1 + exp((-5 - V) / 14))

#
# Background Sodium current INaB
#
# Appendix to [3]
#
[inab]
use membrane.V
gNaB = 0.00029 [mS/uF]
INaB = gNaB * (V - rev.ENa)
    in [A/F]

#
# Sodium-Calcium exchanger INaCa
#
# Appendix to [3]
#
[inaca]
use phys.FTR
use membrane.V
use sodium.Na_i, extra.Nao
use calcium.Ca_i, extra.Cao
K_NaCa = 1000 in [A/F]
K_sat = 0.1
Km_Ca = 1.38 in [mM]
Km_Nai = 87.5 in [mM]
a = 2.5
g = 0.35
Nao3 = Nao * Nao * Nao
Nai3 = Na_i * Na_i * Na_i
INaCa = K_NaCa * (exp(g * V * FTR) * Nai3 * Cao - exp((g - 1) * V * FTR) * Nao3 * Ca_i * a) / (
        (Km_Nai ^ 3 + Nao3) * (Km_Ca + Cao) * (1 + K_sat * exp((g - 1) * V * FTR)))
    in [A/F]

#
# Sodium-Potassium pump INaK
#
# Appendix to [3]
#
[inak]
use membrane.V
use extra.Ko
use sodium.Na_i
K_mNa = 40 [mM]
K_mk = 1 [mM]
P_NaK = 2.724 [A/F]
INaK = P_NaK * Ko / (Ko + K_mk) * Na_i / (Na_i + K_mNa) / (
        1 + 0.1245 * exp(-0.1 * V * phys.FTR) + 0.0353 * exp(-V * phys.FTR))
    in [A/F]

#
# Sodium dynamics
#
# Appendix to [3]
#
[sodium]
ItNa = ina.INa + inab.INaB + 3 * inak.INaK + 3 * inaca.INaCa
dot(Na_i) = -ItNa / (phys.Vc * phys.F) * phys.Cm
    in [mM]

#
# Potassium Dynamics
#
# Appendix to [3]
#
[potassium]
use membrane.V
ItK = ik1.IK1 + ito.ITo + ikr.IKr + iks.IKs + ipk.IpK - 2 * inak.INaK
dot(K_i) = -(ItK + stimulus.i_stim) / (phys.Vc * phys.F) * phys.Cm
    in [mM]

#
# Calcium dynamics
#
# Appendix to [2]
#
[calcium]
use membrane.V
ItCa = icab.ICaB + ipca.IpCa + ical.ICaL - 2 * inaca.INaCa
    desc: Total Calcium current
V_sr = 0.001094 [um^3]
dot(Ca_i) = Ca_i_bufc * ((i_leak - i_up) * V_sr / phys.Vc + i_xfer - (ItCa - ical.ICaL) * phys.Cm / (2 * phys.Vc * phys.F))
    in [mM]
    Ca_i_bufc = 1 / (1 + Buf_c * K_buf_c / (Ca_i + K_buf_c) ^ 2)
    Buf_c = 0.2 [mM]
    K_buf_c = 0.001 [mM]
dot(Ca_ss) = Ca_ss_bufss * (-ical.ICaL * phys.Cm / (2 * V_ss * phys.F) + i_rel * V_sr / V_ss - i_xfer * phys.Vc / V_ss)
    in [mM]
    Ca_ss_bufss = 1 / (1 + Buf_ss * K_buf_ss / (Ca_ss + K_buf_ss) ^ 2)
    Buf_ss = 0.4 [mM]
    K_buf_ss = 0.00025 [mM]
    V_ss = 5.468e-5 [um^3]
dot(Ca_sr) = Ca_sr_bufsr * (i_up - (i_rel + i_leak))
    in [mM]
    Ca_sr_bufsr = 1 / (1 + Buf_sr * K_buf_sr / (Ca_sr + K_buf_sr) ^ 2)
    Buf_sr = 10 [mM]
    K_buf_sr = 0.3 [mM]
K_up = 0.00025 [mM]
kcasr = max_sr - (max_sr - min_sr) / (1 + (EC / Ca_sr) ^ 2)
EC = 1.5 [mM]
max_sr = 2.5
min_sr = 1
O = k1 * Ca_ss ^ 2 * R_prime / (k3 + k1 * Ca_ss ^ 2)
    k1 = k1_prime / kcasr
    k1_prime = 0.15
    k3 = 0.06 [mS/uF]
dot(R_prime) = -k2 * Ca_ss * R_prime + k4 * (1 - R_prime)
    k2 = k2_prime * kcasr
    k2_prime = 0.045
    k4 = 0.005 [mS/uF]
i_leak = V_leak * (Ca_sr - Ca_i)
    V_leak = 0.00036 [mS/uF]
i_rel = V_rel * O * (Ca_sr - Ca_ss)
    V_rel = 0.102 [mS/uF]
i_up = Vmax_up / (1 + K_up ^ 2 / Ca_i ^ 2)
    Vmax_up = 0.006375 [mol/m^3/ms]
i_xfer = V_xfer * (Ca_ss - Ca_i)
    V_xfer = 0.0038 [mS/uF]

[[protocol]]
# Level  Start    Length   Period   Multiplier
1        1        2        2000     1

[[script]]
import numpy as np
import myokit

#
# In this example, a 2d tissue model with transmural and longitudinal
# heterogeneity is set up.
# A working OpenCL installation is required to run this example.
#
# This example has been tested with Myokit 1.21.8
#

# Reduced gap junction expression
gx = 2
gy = 4

# Tissue dimensions
n_end = 60      # 60  0-59
n_mid = 45      # 45  60-104
n_epi = 60      # 60  105-164
ny = n_end + n_mid + n_epi
nx = 400 

# Get model and protocol, create simulation
m = get_model()
p = get_protocol()
   
# Create a multi-cell simulation
s = myokit.SimulationOpenCL(m, p, ncells=(nx, ny))

# Configure the simulation
s.set_step_size(0.002)
s.set_conductance(gx, gy)

# Endocardial pacing
s.set_paced_cells(nx, 2)

# Add a scalar field representing the cell type (0:endo, 1:epi, 2:mid)
types = np.array([0] * n_end + [2] * n_mid + [1] * n_epi)
types = np.vstack([types] * nx).transpose()
s.set_field('cell.type', types)

# Add heterogenous ical
nx1 = int(0.1 * nx)
nx2 = nx - nx1
gcal = np.array([3.0e-5] * nx1 + [1.2e-5] * nx2)
gcal = np.vstack([gcal] * ny)
s.set_field('ical.gCaL', gcal)

# Run logged simulation
d = s.run(700, log_interval=5, log=['engine.time', 'membrane.V'])

# Create DataBlock1d from results, save to disk for inspection with Viewer
b = d.block2d()
b.save('ttp-transmural-plane.zip')


